buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'groovy'
    id 'eclipse'
    id 'signing'
    id 'maven'
    id 'jacoco'
    id 'com.github.hierynomus.license' version '0.14.0'
}

tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }
tasks.withType(GroovyCompile) { options.encoding = 'UTF-8' }

group = 'com.blackducksoftware.integration'

version = '1.0.0-SNAPSHOT'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    compile 'com.blackducksoftware.integration:integration-bdio:5.0.0-SNAPSHOT'
    compile 'org.codehaus.groovy:groovy-all:2.4.11'
}

license {
    header = file('HEADER')
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    ignoreFailures = true
    includes (["**/*.groovy", "**/*.java"])
    exclude "/src/test/*"
}

tasks.licenseMain.dependsOn(licenseFormatMain)

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment ->
                signing.signPom(deployment)
            }
            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            pom.project {
                name rootProject.name
                description rootProject.description
                url 'https://www.github.com/blackducksoftware/hub-detect-model'
                packaging 'jar'
                scm {
                    connection 'scm:git:git://github.com/blackducksoftware/hub-detect-model.git'
                    developerConnection 'scm:git:git@github.com:blackducksoftware/hub-detect-model.git'
                    url 'https://www.github.com/blackducksoftware/hub-detect-model'
                }
                licenses {
                    license {
                        name 'Apache License 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
                developers {
                    developer {
                        id 'blackduckoss'
                        name 'Black Duck OSS'
                        email 'bdsoss@blackducksoftware.com'
                        organization 'Black Duck Software, Inc.'
                        organizationUrl 'http://www.blackducksoftware.com'
                        roles { role 'developer' }
                        timezone 'America/New_York'
                    }
                }
            }
        }
    }
}

gradle.taskGraph.whenReady {
    if (gradle.taskGraph.hasTask(":uploadArchives")) {
        println "Uploading ${rootProject.name} version ${version} to maven central."
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task printVersion {
    doLast{
       println project.version
    }
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}
